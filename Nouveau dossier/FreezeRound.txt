ACTOR M79FreezeProj
{
	Radius 4
	Height 8
	Speed 50//30
	Damage 0
	Mass 40
	PROJECTILE
	-NOGRAVITY
	+RANDOMIZE
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	+SHOOTABLE
	+SKYEXPLODE
	DeathSound "Weapons/M79/Freeze"
	Obituary "$OB_GRENADE"
	DamageType Ice
	Gravity 0.85
	Scale 0.65
	States
	{
	Spawn:
		GRND A 1
		Loop
	Death:
		NULL A 0 BRIGHT A_StopSound(CHAN_BODY)
		NULL A 0 BRIGHT A_Explode(1, 128, 0, False, 128, 0, 0, None, Normal) // Break frozen things.
		NULL A 0 BRIGHT
		{
			A_ChangeFlag("NODAMAGETHRUST", False);
			A_Explode(50*random(1,4),16,0); // Simulated impact damage
			A_Explode(128, 128, 0); // Freezy radius damage.
		}
		NULL AAAAAAAA 0 BRIGHT A_SpawnItemEx("CryoSmoke", 0, 0, 0, random(10, 30)*0.1, 0, random(0, 10)*0.1, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
		NULL AAAA 0 BRIGHT A_SpawnItemEx("CryoSmoke2", 0, 0, 0, random(10, 30)*0.04, 0, random(0, 10)*0.04, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION,64)
		NULL AAAA 0 BRIGHT A_SpawnItemEx("CryoExplosionFake",0,0,0)
		TNT1 A 1
		Stop
	}
}

ACTOR CryoExplosionFake
{
    Radius 2
    Height 2
    +NOINTERACTION
    +FORCEXYBILLBOARD
    RenderStyle "Add"
    Alpha 0.1
    States
    {
    Spawn:
		TNT1 A 1
		WGIX ABCDEFG 1 BRIGHT
		WGIX HIJKLM 1 BRIGHT A_FadeOut(0.1)
        Wait
    }
}

ACTOR CryoSmoke
{
    Radius 2
    Height 2
    +NOINTERACTION
    +FORCEXYBILLBOARD
    RenderStyle "Shaded"
    StencilColor "A0 A0 A0"
    Alpha 1.0
    Scale 0.4
    States
    {
    Spawn:
		NULL A 0
        WGJS A 2 A_FadeOut(0.05)
        Wait
    }
}

Actor CryoSmoke2 : CryoSmoke
{
	StencilColor "DD DD DD"
	VSpeed 2.5
	Scale 0.7
	Alpha 0.5
	States
	{
	Spawn:
		NULL A 0
		WGJS A 6 A_FadeOut(0.04)
		Wait
	}
}