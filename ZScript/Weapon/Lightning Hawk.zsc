Class LightningHawk : REWeapon
{
	Default
	{
		Inventory.Icon "WP2PA0";

		Weapon.SelectionOrder 1900;
		Weapon.AmmoType "WP2Ammo";
		Weapon.AmmoType2 "MagnumAmmo"; // .44 Magnum
		+WEAPON.NOALERT
		Inventory.PickupMessage "Lightning Hawk";
		Obituary "$OB_MPPISTOL";
		Tag "Lightning Hawk";
		Scale 0.2;
	}
	Override Void AttachToOwner(Actor other)
	{
		Super.AttachToOwner(other);
		other.A_GiveInventory("WP2Ammo",MAG_DE);
	}
   	Override Bool TryPickup(in out Actor toucher)
    {
		If(toucher)
		{
			Let weap = toucher.FindInventory("LightningHawk");
			If(!weap) { Self.AmmoGive2 = 0; }
			Else { Self.AmmoGive2 = MAG_DE; }
		}
			Return Super.TryPickup(toucher);
	}
	States
	{
	Spawn:
		WP2P A 9;
		#### # 1 Bright;
		Loop;
	Select:
		WP2G B 0 A_JumpIf(!CountInv("WP2Ammo"), "Raise");
		WP2G A 0;
		Goto Raise;
	Ready:
		WP2G B 0 A_JumpIf(!CountInv("WP2Ammo"), 2);
		WP2G A 0;
		WP2G # 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop;
	Deselect:
		WP2G B 0 A_JumpIf(!CountInv("WP2Ammo"), "Lower");
		WP2G A 0;
		Goto Lower;
	Fire:
		TNT1 A 0 A_JumpIf(!CountInv("WP2Ammo"), "Reload");
		WP2F A 3
		{
			A_AlertMonsters();
			A_FireBullets(0.4, 0.4, 1, random(40,75), "BulletPuff_2");
			A_StartSound("Weapons/DE50/Fire", CHAN_WEAPON);

			A_SetPitch(Pitch - 0.72, SPF_INTERPOLATE); // -72 % = 28 pts of stability
			A_QuakeEX(4,4,4,5,0,10, "None", QF_SCALEDOWN);

			A_TakeInventory("WP2Ammo",1);
		}
		#### B 3;
		#### C 3 A_SpawnItemEx("RE_MagnumCasing",24,-24,40*GetCrouchFactor(AAPTR_Default),8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
		#### D 3 A_JumpIf(!CountInv("WP2Ammo"), "Ready");
		#### E 3;
		Goto Ready;
	Flash:
		TNT1 A 0;
		Goto LightDone;

	Reload:
		TNT1 A 0 A_JumpIfInventory("WP2Ammo", MAG_DE, 2);
		TNT1 A 0 A_JumpIf(CountInv("MagnumAmmo") > 0 || CountInv("PowerInfiniteAmmo"), "ReloadWork"); 
		TNT1 A 0;
		Goto Ready;
	ReloadWork:
		WP2R ABCD 3;
		#### E 3 A_StartSound("Weapons/DE50/Unload", CHAN_WEAPON);
		#### FGHIJ 3;
		#### K 3 A_StartSound("Weapons/DE50/Reload", CHAN_WEAPON);
		#### LMNOP 3;
	ReloadLoop:
		TNT1 A 0
		{
            While(CountInv("MagnumAmmo") >= 0 && CountInv("WP2Ammo") < MAG_DE)
			{
				If(!CountInv("PowerInfiniteAmmo")) { TakeInventory("MagnumAmmo", 1, False, True); }
				GiveInventory("WP2Ammo", 1);
			}
		}
		TNT1 A 0;
		Goto Ready;
	}
}

Class WP2Ammo : Ammo
{
	Default
	{
		Inventory.MaxAmount MAG_DE;
		+INVENTORY.IGNORESKILL
	}
}
