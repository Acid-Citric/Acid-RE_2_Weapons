Class Matilda : REWeapon
{	
	Default
	{
		Inventory.Icon "WP1PA0";

		Weapon.SelectionOrder 1900;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive2 MAG_VP70;
		Weapon.AmmoType "WP1Ammo";
		Weapon.AmmoType2 "HandgunAmmo";
		+WEAPON.NOALERT
		Inventory.PickupMessage "H&K VP70";
		Obituary "$OB_MPPISTOL";
		Tag "H&K VP70";
		Scale 0.2;
		+INVENTORY.UNDROPPABLE
	}
	Override Void AttachToOwner(Actor other)
	{
		Super.AttachToOwner(other);
		other.A_GiveInventory("WP1Ammo",MAG_VP70);
	}
    Override Bool TryPickup(in out Actor toucher) 
    {
		If(toucher)
		{
			Let weap = toucher.FindInventory("Matilda");
			If(!weap) { Self.AmmoGive2 = 0; }
		}
			Return Super.TryPickup(toucher);
    }
	States
	{
	Spawn:
		WP1P A 30;
		WP1P A 2 Bright;
		Loop;
	Select:
		WP1G A 0;
		Goto Raise;
	Ready:
		WP1G A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop;
	Deselect:
		"####" "#" 0;
		Goto Lower;
	Fire:
		TNT1 A 0 A_JumpIf(!CountInv("WP1Ammo"), "Reload");
		WP1F A 2 Bright
		{
			A_AlertMonsters();
			A_FireBullets(0, 0, 1, 7, "BulletPuff");
			A_StartSound("Weapons/VP70/Fire", CHAN_WEAPON);

			A_SetPitch(Pitch - 0.4, SPF_INTERPOLATE); // -40 % = 60 pts of stability

			A_TakeInventory("WP1Ammo",1);
		}
		WP1F B 1;
		WP1F C 1 A_FireProjectile("RE_PistolCasing_Spawner", 345+random(-8,8), False, -4, -5, 0);
		WP1F B 1;
		WP1G A 2;
		Goto Ready;
	Flash:
		TNT1 A 0;
		Goto LightDone;

	Reload:
		TNT1 A 0 A_JumpIf(CountInv("WP1Ammo") == MAG_VP70, 2);
		TNT1 A 0 A_JumpIf(CountInv("HandgunAmmo") > 0 || CountInv("PowerInfiniteAmmo"), "ReloadWork"); // sv_infiniteammo
		TNT1 A 0;
		Goto Ready;
	ReloadWork:
		WP1R A 2; 
		WP1R B 2 A_StartSound("Weapons/VP70/Unload", CHAN_WEAPON);
		WP1R C 5;
		WP1R DEFG 2;
		WP1R H 2 A_StartSound("Weapons/VP70/Reload", CHAN_WEAPON);
		WP1R IJKLMNOPQ 2;
		WP1G A 1;
	ReloadLoop:
		TNT1 A 0
		{
            While(CountInv("HandgunAmmo") > 0 && CountInv("WP1Ammo") < MAG_VP70 || CountInv("PowerInfiniteAmmo")  && CountInv("WP1Ammo") < MAG_VP70)
			{ // sv_infiniteammo
				A_TakeInventory("HandgunAmmo",1,TIF_NOTAKEINFINITE);
				A_GiveInventory("WP1Ammo",1);
			}
		}
		TNT1 A 0;
		Goto Ready;
	}
}

Class WP1Ammo : Ammo
{
	Default
	{
		Inventory.MaxAmount MAG_VP70;
		+INVENTORY.IGNORESKILL
	}
}