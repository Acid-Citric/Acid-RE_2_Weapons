Class MQ11 : REWeapon
{
	Default
	{
		Inventory.Icon "WP4PA0";

		Weapon.SelectionOrder 700;
		Weapon.AmmoType "WP4Ammo";
		Weapon.AmmoType2 "SubmachineGunAmmo";
		+WEAPON.NOAUTOFIRE
		+WEAPON.NOALERT
		Inventory.PickupMessage "MQ 11";
		Obituary "$OB_MPMACHINEGUN";
		Tag "MQ 11";
		Scale 0.3;
	}
	Override Void AttachToOwner(Actor other)
	{
		Super.AttachToOwner(other);
		other.A_GiveInventory("WP4Ammo",MAG_MAC11);
	}
	Override Bool TryPickup(in out Actor toucher)
    {
		If(toucher)
		{
			Let weap = toucher.FindInventory("MQ11");
			If(!weap) { Self.AmmoGive2 = 0; }
			Else { Self.AmmoGive2 = MAG_MAC11; }
		}
			Return Super.TryPickup(toucher);
   	}
	States
	{
	Spawn:
		WP4P A 9;
		#### # 1 Bright;
		Loop;
	Select:
		WP4G B 0 A_JumpIf(!CountInv("WP4Ammo"), "Raise");
		WP4G A 0;
		Goto Raise;
	Ready:
		WP4G B 0 A_JumpIf(!CountInv("WP4Ammo"), 2);
		WP4G A 0;
		#### # 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop;
	Deselect:
		WP4G B 0 A_JumpIf(!CountInv("WP4Ammo"), "Lower");
		WP4G A 0;
		Goto Lower;
	Fire:
		TNT1 A 0 A_JumpIf(!CountInv("WP4Ammo"), "Reload");
		WP4F A 1
		{
			A_AlertMonsters();
			A_FireBullets(1.8, 1.8, 1, random(8,13),  "BulletPuff_2", FBF_NORANDOM);
			A_StartSound("Weapons/MAC11/Fire", CHAN_WEAPON);

			A_SetPitch(Pitch - 0.36, SPF_INTERPOLATE); // -36 % = 64 pts of stability
			A_QuakeEx(1,1,1,5,0,96,"None");

			A_TakeInventory("WP4Ammo",1);
		}
		#### B 1 A_SpawnItemEx("RE_PistolCasing",24,-24,40*GetCrouchFactor(AAPTR_Default),8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
		#### C 1;
		#### D 1;
		#### A 0 A_ReFire();
		Goto Ready;
	Flash:
		TNT1 A 0;
		Goto LightDone;

	Reload:
		TNT1 A 0 A_JumpIfInventory("WP4Ammo", MAG_MAC11, 2);
		TNT1 A 0 A_JumpIf(CountInv("SubmachineGunAmmo") > 0 || CountInv("PowerInfiniteAmmo"), "ReloadWork"); 
		TNT1 A 0;
		Goto Ready;
	ReloadWork:
		WP4R AB 2;
		#### C 2 A_StartSound("Weapons/MAC11/Unload", CHAN_WEAPON);
		#### DEFG 2;
		#### H 2 A_StartSound("Weapons/MAC11/Unload", CHAN_WEAPON);
		#### IJ 2;
		#### K 4;
		#### LM 3;
		#### N 3 A_StartSound("Weapons/MAC11/Reload", CHAN_WEAPON);
		#### OPQ 3;
		#### R 3;
		#### STUV 3;
		WP4G A 3;
	ReloadLoop:
		TNT1 A 0
		{
           	While(CountInv("SubmachineGunAmmo") >= 0 && CountInv("WP4Ammo") < MAG_MAC11)
			{
				If(!CountInv("PowerInfiniteAmmo")) { A_TakeInventory("SubmachineGunAmmo", 1, TIF_NOTAKEINFINITE); }
				A_GiveInventory("WP4Ammo", 1);
			}
		}
		TNT1 A 0;
		Goto Ready;
	}
}

Class WP4Ammo : Ammo
{
	Default
	{
		Inventory.MaxAmount MAG_MAC11;
		+INVENTORY.IGNORESKILL
	}
}
