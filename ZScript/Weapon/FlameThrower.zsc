Class ChemicalFlameThrower : REWeapon // "Kill it with fire before it lays eggs!"
{
	Default
	{
		Inventory.Icon "WP6PA0";

		Weapon.SelectionOrder 100;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive2 MAG_FLAME;
		Weapon.AmmoType "WP6Ammo";
		Weapon.AmmoType2 "Fuel";
		Inventory.PickupMessage "FlameThrower";
		Tag "FlameThrower";
		Scale 0.3;
	}
	Override Void AttachToOwner(Actor other)
	{
		Super.AttachToOwner(other);
		other.A_GiveInventory("WP6Ammo",MAG_FLAME);
	}
    Override Bool TryPickup(in out Actor toucher) 
    {
		If(toucher)
		{
			Let weap = toucher.FindInventory("ChemicalFlameThrower");
			If(!weap) { Self.AmmoGive2 = 0; }
		}
			Return Super.TryPickup(toucher);
    }
	States
	{
	Spawn:
		WP6P A 30;
		WP6P A 2 Bright;
		Loop;
	Select:
		WP6G A 0;
		Goto Raise;
	Ready:
		WP6G A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop;
	Deselect:
		"####" "#" 0;
		Goto Lower;
	Fire:
		TNT1 A 0 A_JumpIf(WaterLevel >= 2, "Empty");
		TNT1 A 0 A_JumpIf(!CountInv("WP6Ammo"), "Reload");
		WP6G A 1 Bright
		{
			A_AlertMonsters();
			A_FireProjectile("FlameThrowerBall",2,True,2,5);
			A_TakeInventory("WP6Ammo",2);
			A_WeaponOffset(random(0,1),random(32,34),WOF_INTERPOLATE);
			
			A_SetPitch(Pitch - 1.0);
		}
		"####" A 1 Bright A_WeaponOffset(random(0,1),random(32,33),WOF_INTERPOLATE);
		//"####" A 0 A_ReFire();
		Goto Ready;

	Empty:
		WP6G A 0 A_StartSound("Weapons/Flamer/Dry", CHAN_WEAPON);
		WP6G A 3 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;

	Reload:
		TNT1 A 0 A_JumpIfInventory("WP6Ammo", MAG_FLAME, "Ready");
		TNT1 A 0 A_JumpIf(CountInv("Fuel") > 0 || CountInv("PowerInfiniteAmmo"), "ReloadWork"); // sv_infiniteammo
		TNT1 A 0;
		Goto Empty;
	ReloadWork:
		// 80 tics
		WP6G AAAAAAA 1 // 7 tics
		{
			//Tilt the gun to the right, offset and scale it:
			A_OverlayRotate(PSP_WEAPON,-2,WOF_ADD);
			A_OverlayOffset(PSP_WEAPON,6,3,WOF_ADD);
			A_OverlayScale(PSP_WEAPON,0.015,0.015,WOF_ADD);
		}
		WP6G B 35 A_StartSound("weapons/sshoto",CHAN_AUTO);
		WP6G B 15 A_StartSound("weapons/sshotl",CHAN_AUTO);
		WP6G B 0
		{
			While(CountInv("Fuel") > 0 && CountInv("WP6Ammo") < MAG_FLAME || CountInv("PowerInfiniteAmmo") && CountInv("WP6Ammo") < MAG_FLAME)	
			{ // sv_infiniteammo
				A_TakeInventory("Fuel", 1, TIF_NOTAKEINFINITE);
				A_GiveInventory("WP6Ammo", 1);
			}
		}
		WP6G B 16 A_StartSound("weapons/sshotc",CHAN_AUTO);
		WP6G AAAAAAA 1 // 7 tics
		{
			//Restore the gun's angle, offset and scale:
			A_OverlayRotate(PSP_WEAPON,2,WOF_ADD);
			A_OverlayOffset(PSP_WEAPON,-6,-3,WOF_ADD);
			A_OverlayScale(PSP_WEAPON,-0.015,-0.015,WOF_ADD);
		}
		Goto Ready;
	}
}

Class WP6Ammo : Ammo
{
	Default
	{
		Inventory.MaxAmount MAG_FLAME;
		+INVENTORY.IGNORESKILL
	}
}

Class FlameThrowerBall : FastProjectile
{
	Default
	{
		SeeSound "Weapons/Flamer/Fire"; //"weapons/rocklf"
		DeathSound "Weapons/Flamer/Hit"; //"vile/firecrkl"

		+DONTSPLASH
		+NOGRAVITY
		+NOEXTREMEDEATH
		+BLOODLESSIMPACT
		Speed 20;
		Height 6;
		Radius 5;
		Mass 10;
		Damage 6;
		DamageType "Fire";
		PROJECTILE;
		MaxStepHeight 4;
		RenderStyle "Add";
		Scale 0.25; // 0.75
		Alpha 1;
	}
	States
	{
	Spawn:
        TNT1 A 2;
        WFKB ABCDEFGHIJKLMNOP 1 Bright A_SetScale(Scale.X + 0.025, Scale.Y + 0.025);
		Stop;
	}
}